group 'me.fru1t.stak'
version '0.1-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

buildscript {
    ext.kotlin_version = '1.2.41'
    ext.junit_version = '1.0.2'
    ext.junit_jupiter_version = '5.0.2'
    ext.testfx_version = '4.0.10-alpha'
    ext.dagger_version = '2.16'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_version"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    compile group: 'com.google.guava', name: 'guava', version: '22.0'
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.12'
    compile group: 'com.google.dagger', name: 'dagger', version: dagger_version
    compile project(':libs:fm-stak-server')

    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_jupiter_version
    testCompile group: 'com.google.truth', name: 'truth', version: '0.33'
    testCompile group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.5.0'
    testCompile group: 'org.testfx', name: 'testfx-core', version: testfx_version
    testCompile group: 'org.testfx', name: 'testfx-junit5', version: testfx_version

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_jupiter_version
}

junitPlatform {
    platformVersion junit_version
    enableStandardTestTask true
}

sourceCompatibility = 1.8
mainClassName = "me.fru1t.stak.StakApplicationKt"
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

idea {
    module {
        sourceDirs += file('build/generated/source/kapt/main')
        generatedSourceDirs += file('build/generated/source/kapt/main')
    }
}

// Produces a runnable output jar with all dependencies packaged in
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Stak',
                'Implementation-Version': version,
                'Main-Class': 'me.fru1t.stak.StakApplicationKt'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
